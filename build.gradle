import java.text.SimpleDateFormat

plugins {
    id 'application'
}

group 'com.reportmill'
version new SimpleDateFormat("yyyy.MM").format(new Date())

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['src']

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {

    // SnapKit project
    implementation project(':SnapKit')

    implementation group: 'org.jbox2d', name: 'jbox2d-library', version: '2.2.1.1'
}

// Define the main class for the application
mainClassName = 'snapdemos.arcade.ArcadeApp'

jar {
    manifest {
        attributes 'Main-Class': 'snapdemos.tetris.TetrisPane'
    }
}

/**
 * Custom task to build jar that includes all dependencies.
 */
task buildFullJar(type: Jar) {
    archiveBaseName = 'SnapDemosAll'
    manifest {
        attributes(
                'Main-Class': 'snapdemos.arcade.ArcadeApp',
                'Application-Name': 'SnapDemos',
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('**/org/**', '**/test/**', '**/sun/**')
    with jar
    dependsOn build
}

task copyCJ(type: Copy) {
    from (
            "../SnapKit/cjdom.js",
            "../SnapKit/build/libs/SnapKit-" + new SimpleDateFormat("yyyy.MM").format(new Date()) + ".jar",
    )
    into "build/libs"
    dependsOn buildFullJar

    // Declare that this task depends on the 'jar' task of WebAPI, SnapCJ
    //dependsOn(":WebAPI:jar", ":SnapCJ:jar")
}
